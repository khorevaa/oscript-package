#Использовать configor
// #Использовать logos

Перем мПараметры;

Функция Параметры() Экспорт
	Возврат мПараметры;
КонецФункции

Процедура УстановитьПараметры(Знач ВходящиеПараметры) Экспорт
	мПараметры = ВходящиеПараметры;
КонецПроцедуры

Процедура ОписаниеПараметров(Знач КонструкторПараметров) Экспорт
	
	Репозиторий = КонструкторПараметров.НовыеПараметры()
				.ПолеСтрока("Тип type", "git")
				.ПолеСтрока("Адрес url")
				;
	
	Автор = КонструкторПараметров.НовыеПараметры()
				.ПолеСтрока("Имя name")
				.ПолеСтрока("Емайл email")
				.ПолеСтрока("ИнтернетСтраница url")
				.Конструктор()
				;
	Разработчики = Автор.Скопировать();
	
	Зависимости = КонструкторПараметров.НовыеПараметры()
				.ПроизвольныеПоля()
				;
	ЗависимостиРазработки = Зависимости.Скопировать();
	
	Задачи = КонструкторПараметров.НовыеПараметры()
				.ПолеСтрока("СкриптСборки build", "opm build .")
				.ПроизвольныеПоля()
				;
	Классы = КонструкторПараметров.НовыеПараметры()
				.ПроизвольныеПоля()
				;
	Модули = КонструкторПараметров.НовыеПараметры()
				.ПроизвольныеПоля()
				;

	ИсполняемаяСреда = КонструкторПараметров.НовыеПараметры()
				.ПолеСтрока("oscript", "*")
				.ПолеСтрока("mono")
				;

	ИсполняемыеФайлы = КонструкторПараметров.НовыеПараметры()
				.ПроизвольныеПоля()
				;

	ОбъявлениеМодулей = КонструкторПараметров.НовыеПараметры()
				.ПолеОбъект("Классы classes", Классы)
				.ПолеОбъект("Модули modules", Модули)
				;

	КонструкторПараметров
				.ПолеСтрока("Имя name")
			   	.ПолеСтрока("Версия version")
				.ПолеСтрока("Описание description")
				.ПолеСтрока("ВерсияСреды engine-version", "*")
				.ПолеОбъект("ИсполняемаяСреда engine", ИсполняемаяСреда)
				.ПолеСтрока("Лицензия license", "ISC")
				.ПолеСтрока("ДомашняяСтраница homepage")
				.ПолеСтрока("ТрекерОшибок bugs")
				.ПолеСтрока("ТочкаВхода main")
				.ПолеМассив("ВключаемыеФайлы include-content", Тип("Строка"))
				.ПолеОбъект("ОбъявлениеМодулей explicit-modules", ОбъявлениеМодулей)
				.ПолеМассив("КлючевыеСлова keywords", Тип("Строка"))
				.ПолеОбъект("Зависимости dependencies depends-on", Зависимости)
				.ПолеОбъект("ЗависимостиРазработки devDependencies", ЗависимостиРазработки)
				.ПолеОбъект("Репозиторий repository", Репозиторий)
				.ПолеОбъект("Автор author", Автор)
				.ПолеМассив("Разработчики contributors", Разработчики)
				.ПолеОбъект("Задачи tasks scripts", Задачи)
				.ПолеОбъект("ИсполняемыеФайлы executable", ИсполняемыеФайлы)
				;

КонецПроцедуры

Лог = Логирование.ПолучитьЛог("oscript.lib.packagedef.constructor");